apply from: "../commons-limited-device.gradle"

android {
    defaultConfig {
        applicationId "org.torproject.android.tv"
        minSdkVersion 23 
        targetSdkVersion 29
    }

    aaptOptions {
        cruncherEnabled = false
    }

    flavorDimensions "teevee"

    productFlavors {
        teevee {
            dimension "teevee"
            minSdkVersion 23 
            applicationId 'org.torproject.android.tv'
            targetSdkVersion 29 
            versionCode 10020000
            versionName 'orbot-tv-1.0.0-BETA-2-tor-0.4.5.9'
            archivesBaseName = "Orbot-TV-$versionName"
        }
    }

    packagingOptions {
        exclude 'lib/x86/obfs4proxy.so'
        exclude 'lib/x86_64/obfs4proxy.so'
        exclude 'lib/arm64-v8a/obfs4proxy.so'
        exclude 'lib/armeabi-v7a/obfs4proxy.so'
        exclude 'lib/armeabi/obfs4proxy.so'
    }
}

dependencies {
    implementation(
            project(path: ':appcore'),
            project(path: ':intentintegrator'),
            project(':orbotservice'),
            'androidx.constraintlayout:constraintlayout:2.1.0',
            'androidx.coordinatorlayout:coordinatorlayout:1.1.0',
            "androidx.leanback:leanback:1.2.0-alpha01",
            "androidx.leanback:leanback-paging:1.1.0-alpha08",
            "androidx.leanback:leanback-preference:1.2.0-alpha01",
            "androidx.leanback:leanback-tab:1.1.0-beta01",
            'androidx.palette:palette:1.0.0',
            'androidx.recyclerview:recyclerview:1.2.1',
            'com.github.apl-devs:appintro:v4.2.2',
            'com.github.jetradarmobile:android-snowfall:1.2.1',
            'com.google.android.material:material:1.4.0'
    )
    androidTestImplementation "tools.fastlane:screengrab:1.2.0"

}

// For each APK output variant, override version code of outputs based on ABI codes
// ex) 'mips' -> 3xxx
// ex) 'x86' -> 4xxx
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseVersionCode = project.ext.abiCodes.get(output.getFilter(com.android.build.OutputFile.ABI))
        if (baseVersionCode != null) {
            output.versionCodeOverride = Integer.valueOf(baseVersionCode + variant.versionCode)
        }
    }
}
