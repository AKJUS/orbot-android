apply from: "../commons-device.gradle"

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    defaultConfig {
        applicationId "org.torproject.android"
        versionName getVersionName()
        minSdkVersion 16
        targetSdkVersion 30
    }
    lintOptions {
        htmlReport true
        xmlReport false
        textReport false
        lintConfig file("../lint.xml")
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
    }

    flavorDimensions "free"

    productFlavors {
        fullperm {
            dimension "free"

            minSdkVersion 16
            applicationId 'org.torproject.android'
            targetSdkVersion 29
            versionCode 1651100210

            versionName getVersionName()
            archivesBaseName = "Orbot-$versionName"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }

    dependencies {
        implementation(
                project(':appcore'),
                project(':intentintegrator'),
                project(':orbotservice'),
                libs.apl.appintro,
                libs.android.snowfall,
                libs.android.volley,
                libs.androidx.core,
                libs.androidx.localbroadcast,
                libs.androidx.palette,
                libs.android.material,
                libs.bclogic.pulsator,
                libs.guardian.jtorctl,
                libs.ipt.proxy

        )
        androidTestImplementation libs.fastlane.screengrab
    }
}