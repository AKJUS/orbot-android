apply from: "../commons-device.gradle"

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    defaultConfig {
        applicationId "org.torproject.android"
        versionName getVersionName()
        minSdkVersion 16
        targetSdkVersion 30
    }
    lintOptions {
        htmlReport true
        xmlReport false
        textReport false
        lintConfig file("../lint.xml")
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
    }

    flavorDimensions "free"

    productFlavors {
        fullperm {
            dimension "free"

            minSdkVersion 16
            applicationId 'org.torproject.android'
            targetSdkVersion 29
            versionCode 1651100210

            versionName getVersionName()
            archivesBaseName = "Orbot-$versionName"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }
}

dependencies {
    implementation(
            project(path: ':appcore'),
            project(path: ':intentintegrator'),
            project(':orbotservice'),
            'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0',
            'com.android.volley:volley:1.2.0',
            'com.github.apl-devs:appintro:v4.2.2',
            'com.github.jetradarmobile:android-snowfall:1.2.1',
            'com.github.tladesignz:IPtProxy:1.0.0',
            'com.google.android.material:material:1.4.0',
            'pl.bclogic:pulsator4droid:1.0.3'
    )
    androidTestImplementation "tools.fastlane:screengrab:1.2.0"
}