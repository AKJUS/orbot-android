/*
    Applies across android app modules: {app, app-mini, app-tv}
 */
apply plugin: 'com.android.application'
apply from: "../commons.gradle"

ext.abiCodes = ['armeabi-v7a': '1', 'arm64-v8a': '2', 'mips': '3', 'x86': '4', 'x86_64': '5']

ext.createKeyStoreProperties = { ->
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def properties = new Properties()
    if (keystorePropertiesFile.canRead()) {
        properties.load(new FileInputStream(keystorePropertiesFile))
    }
    return properties
}

/* gets the version name from the latest Git tag, stripping the leading v off */
ext.getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def keystoreProperties = createKeyStoreProperties()

android {

    signingConfigs {
        release {
            if (!keystoreProperties.stringPropertyNames().isEmpty()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a", "x86_64", "arm64-v8a"
            universalApk false
        }
    }

    packagingOptions {
        exclude 'META-INF/androidx.localbroadcastmanager_localbroadcastmanager.version'
    }
}

repositories {
    jcenter {
        content {
            includeGroup('com.jraska')
            includeGroup('tools.fastlane')
        }
    }
}


